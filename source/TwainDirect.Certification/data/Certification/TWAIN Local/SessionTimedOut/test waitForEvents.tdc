;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Test waitForEvents callback v1.0 04-Sep-2017
; arg:1 - test to run
; arg:2 - variable to set with the result
; returns: pass or fail
;
; Exercise the waitForEvents command.  We're called by other scripts.  This script runs as a callback
; when the waitForEvents thread processes a response.
;
; - confirm that we see sessionTimedOut at some point
;



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; We don't have a banner, because we're called from other scripts...
;
set waitforeventsresultname '${arg:2}'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Dispatch the test...
;
if '${arg:1}' == 'sessiontimedout' goto SESSIONTIMEDOUT
echopassfail 'event waitForEvents - ${arg:1}' 'fail (no test specified)'
return 'fail'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; SESSIONTIMEDOUT, watch for the session to time out.
;
:SESSIONTIMEDOUT
;
; Analyze the result, we have to check all the events...
set ii 0
:SESSIONTIMEDOUT.EVENTSNEXT
	;
	; We've run out of events.
	if '${ej:results.events[${get:ii}]}' == '' goto SESSIONTIMEDOUT.SKIP
	;
	;
	echo 'event waitForEvents - ${ej:results.events[${get:ii}]}'
	;
	; We got a sessionTimedOut event.
	if '${ej:results.events[${get:ii}].event}' == 'sessionTimedOut' goto SESSIONTIMEDOUT.PASS
	;
	; We need to leave.
	if '${ej:results.events[${get:ii}].event}' == 'critical' goto SESSIONTIMEDOUT.FAIL
	;
	; Next event.
	increment ii '${get:ii}' 1
	goto SESSIONTIMEDOUT.EVENTSNEXT
;
; We're good, we found one...
:SESSIONTIMEDOUT.PASS
echopassfail 'event waitForEvents - ${arg:1}' 'pass'
set '${get:waitforeventsresultname}' 'pass'
return 'pass'
;
; We didn't see it this time...
:SESSIONTIMEDOUT.SKIP
return 'skip'
;
; Ruh-roh...
:SESSIONTIMEDOUT.FAIL
echopassfail 'event waitForEvents - ${arg:1}' 'fail'
set '${get:waitforeventsresultname}' 'fail'
return 'fail'
